plugins {
    id 'java'
    id 'jacoco'
    id 'org.springframework.boot' version '3.5.0'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'org.sonarqube' version '6.3.1.5724'
}

java {
    toolchain { languageVersion = JavaLanguageVersion.of(17) }
}

repositories { mavenCentral() }

dependencies {

    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.security:spring-security-crypto'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation 'org.jsoup:jsoup:1.21.2'

    implementation 'org.springframework.boot:spring-boot-starter-mail'


    implementation 'org.flywaydb:flyway-core:11.7.2'
    runtimeOnly   'org.flywaydb:flyway-database-postgresql:11.7.2'
    runtimeOnly 'org.postgresql:postgresql'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage'
    }
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation 'com.h2database:h2'


}


test {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacoco {
    toolVersion = "0.8.12"
}

tasks.jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
}

sonar {
    properties {
        property "sonar.projectKey", "I547859_housinghelper_ae8e7364-5047-4633-b1b8-b09df4130bef"
        property "sonar.projectName", "HousingHelper"
        property "sonar.qualitygate.wait", "true"

        property "sonar.junit.reportPaths", "build/test-results/test"
        property "sonar.coverage.jacoco.xmlReportPaths", "build/reports/jacoco/test/jacocoTestReport.xml"

        property "sonar.host.url", System.getenv("SONAR_HOST_URL")
        if (System.getenv("SONAR_TOKEN") != null) {
            property "sonar.token", System.getenv("SONAR_TOKEN")
        }
    }
}
